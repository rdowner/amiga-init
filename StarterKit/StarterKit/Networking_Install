(procedure P_extract_lha nicename arcname checkdir mustmakedir
    (if (= 0 (exists (tackon #extracts checkdir))) (
        (working (cat "Extracting " nicename " archive"))
        (if (= 0 mustmakedir)
            (set #t_destdir #extracts)
            (
                (set #t_destdir (tackon #extracts extractdir))
                (if (= 0 (exists #t_destdir)) (makedir #t_destdir (infos)))
            )
        )
        (set #t_lhacmd (cat #lha " x " (tackon #archives arcname) " " #t_destdir "/"))
        (set #t_rc
            (run #t_lhacmd
                (prompt (cat "p_Extract " nicename " using LhA command " #t_lhacmd))
                (help (cat "h_Extract " nicename " using LhA command " #t_lhacmd))
            ))
        (if (< 0 #t_rc) (abort (cat "LhA command was unsuccessful: " #t_lhacmd)))
    ))
)

(if (< (/ @installer-version 65536) 43)
    (abort "Please use Install version 43 or later.")
)

(welcome "This installer will prepare your Amiga for the installation of Miami 3.2b, and install some supplemental applications and utilities. After this installer completes, you should run the official Miami installer, then configure Miami.")

(set #basedir "Work:StarterKit")
(set #archives (tackon #basedir "Archives"))
(set #extracts (tackon #basedir "Extracts"))
(set #keyfiles (tackon #basedir "Keyfiles"))
(set #resources (tackon #basedir "Resources"))
(set #lha (tackon (tackon #extracts "LhA") "LhA"))

(P_extract_lha "Miami Core" "Miami32b2-main.lha" "miami32b_install" 0)
(P_extract_lha "Miami GTLayout" "Miami32b-GTL.lha" "miami32b_install/gtlayout" 0)
(set #miamivariant (if (>= (database "cpu") 68020) ("020") ("000")))
(P_extract_lha "Miami Executable" ("Miami32b-%s.lha" #miamivariant) ("miami32b_install/%s" #miamivariant) 0)

(if (= 0 (exists "MIAMI:" (noreq)))
    (
        (makedir "SYS:System/Miami" (infos))
        (makeassign "MIAMI" "SYS:System/Miami")
    )
)
(set @default-dest "MIAMI:")

(if (exists (tackon #keyfiles "Miami.key1")) (copyfiles (source (tackon #keyfiles "Miami.key1")) (dest "MIAMI:")))
(if (exists (tackon #keyfiles "Miami.key2")) (copyfiles (source (tackon #keyfiles "Miami.key2")) (dest "MIAMI:")))

(copyfiles (source (tackon #resources "Start-Miami")) (dest "S:") (infos))
(set #userstartup "Execute S:Start-Miami")

(if (exists (tackon #extracts "AmigaExplorer")) (
    (copyfiles (source (tackon (tackon #extracts "AmigaExplorer") "AExplorer")) (dest "SYS:Utilities") (infos))
    (set #userstartup (cat #userstartup "\n" "Run >NIL: <NIL: AExplorer >NIL: <NIL: CONNECTION TCP/IP QUIET"))
))

(startup "StarterKit_Networking_Install"
    (command #userstartup)
    (prompt "Starts Miami and AmigaExplorer on boot")
    (help "Starts Miami and AmigaExplorer on boot")
)

(exit (cat
    "I've set up as much as I can. You can now run the Miami installer located at "
    (tackon #extracts "Miami32b_Install")
    ". After Miami is installed, open the Miami drawer in the Workbench System drawer. "
    "Run MiamiInit, and then start Miami and import the MiamiInit configuration. "
    "Now Miami should successfully start and go online on every boot."
))

; Couldn't get this to work. Saving it for later...
; (message "I'm going to start the Miami installer now. Once it is complete you will return back to this installer.")
; (set #t_return -1)
; (until (= 0 #t_return)
;     (set #t_cmd (cat "Installer " (tackon (tackon #extracts "Miami32b_Install") "Install_Miami") " MINUSER=EXPERT APPNAME=MIAMI"))
;     (message #t_cmd)
;     (set #t_return (run
;         (cat "Installer " (tackon (tackon #extracts "Miami32b_Install") "Install_Miami") " MINUSER=EXPERT APPNAME=MIAMI")
;         (prompt "Run the Miami installer")
;         (help "Run the Miami installer")
;     ))
;     (if (<> 0 #t_return)
;         (set #t_return (askbool
;             (prompt "The Miami installer didn't seem to work. Do you want to try again?")
;             (help "The Miami installer didn't seem to work. Do you want to try again?")
;             (choices "Retry Miami Install" "Proceed without retry")
;         ))
;     )
; )


